* produces a He-4 gas cell, with a degrader of chosen width and material 
* using a BLtrack file for the beam and has a simple solenoidal field
*
* creates a zntuple, timentuple, profile ntuple, and a beamlossntuple 

physics muCool list=0 disable=Decay
trackcuts keep=mu+ maxTime=1e+9 steppingVerbose=1

param epsMax=1e-3
reference particle=mu+ beamZ=0.0 referenceMomentum=1.0

**********************************Parameters******************************************

param for_file=0

*******************************Beam file**********************************************

param beam_filename=BEAM_100K.txt

************************************Gas cell*******************************************
* cell dimensions in mm defined, and He-4 gas parameters defined

# how far from the start of the world the gas cell will begin
param cell_start=10.0

# length and radius dimensions
param -unset cell_length=500.0
param -unset cell_radius=30.0

# Gas constant cm3*atm/K/mol
param R=82.056

# mass and temperature of He-4
param MHe=4.002602
param He_temp=10

# 50 mbar, converted to atm 
param -unset Pressure=50/1013 
param He_density=($Pressure*$MHe)/($R*$He_temp)

****************************Degrader**************************************************

* degrader thickness in mm

# in mm
param -unset degrader_thickness=0.88

# in K
param degrader_temp=298

*****************************Fields*************************************************

# magnetic fields
param sol_inner_R=$cell_radius+0.1 
param sol_outer_R=$sol_inner_R+2

# electric field / MV/m
param -unset E_field_magnitude=0.1

param -unset position=$cell_start+$cell_length/2

****************************Placing the gas cell*************************************

# define He-4 gas at some density and temperature
material He Z=2 A=$MHe state=g density=$He_density temperature=$He_temp pressure=$Pressure

# creating the gas cell and placing the cell
tubs gas_cell outerRadius=$cell_radius length=$cell_length material=He color=0,1,0
place gas_cell z=$cell_start+($cell_length/2) 

*******************************Placing the degrader************************************

# defining the degrader and placing the degrader
tubs degrader outerRadius=$cell_radius length=$degrader_thickness material=MYLAR color=1,0,0 
place degrader z=0.9*$cell_start

*****************************Fields****************************************************

*****************************Magnetic field********************************************

# defines the solenoidal field parameters, small offset used so that particle trakcs that 
# die on the solenoid are not included in the beamlossntuple


coil default material=Cu 

solenoid default alternate=1 color=0,1,0 kill=1

coil Focus1 innerRadius=$sol_inner_R outerRadius=$sol_outer_R length=$cell_length*2

# need a coil and current, coil defined above as Focus1
solenoid USFocus coilName=Focus1 current=1990 

# field lines shown in visualisation
fieldlines exit=1 center=0,0,$cell_start nLines=100

# adds the solenoid to the world
place USFocus z=($cell_length/2)+$cell_start

********************************Setting up the beam************************************

# more realistic beam
beam ASCII file=$beam_filename beamZ=-$cell_start nEvents=5000

particlecolor mu+=1,0,0

************************Setting up the tuples*****************************************

**************************zntuple*****************************************************

zntuple zloop=$cell_start:($cell_start+$cell_length):10 format=asciiExtended

***********************beamlossntuple trace profile***********************************

beamlossntuple $He_density filename=beamloss require=(z>$cell_start&&z<$cell_start+$cell_length&&(Pz*Pz+Py*Py+Px*Px)<0.0000000001&&PDGid==-13&&(x*x+y*y<$cell_radius*$cell_radius)) format=asciiExtended

if $for_file
 trace nTrace=1000 oneNTuple=1 require=(PDGid==-13) format=for009.dat
else
 trace nTrace=1000 oneNTuple=1 require=(PDGid==-13) format=asciiExtended
endif 

profile filename=profile.txt particle=mu+ zloop=$cell_start:($cell_start+$cell_length):10 coordinates=centerline

*****************************timentuples***********************************************

do tim 0 1000
  timentuple time_output file=time_output time=$tim format=asciiExtended referenceParticle=1
enddo 
